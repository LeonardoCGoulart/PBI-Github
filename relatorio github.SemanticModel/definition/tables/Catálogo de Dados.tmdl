table 'Catálogo de Dados'
	lineageTag: dcd270d6-26d2-49e3-b085-040ba8aec74f

	column Categoria
		lineageTag: 01c1f3da-6969-443c-9e5f-b3749a1cdacd
		summarizeBy: none
		isNameInferred
		sourceColumn: [Categoria]

		annotation SummarizationSetBy = Automatic

	column Nome
		lineageTag: 2c280c84-5c95-413e-8e8d-9d70673fa128
		summarizeBy: none
		isNameInferred
		sourceColumn: [Nome]

		annotation SummarizationSetBy = Automatic

	column Descricao
		lineageTag: c46cb953-3908-462b-beb0-d0da93b69eb4
		summarizeBy: none
		isNameInferred
		sourceColumn: [Descricao]

		annotation SummarizationSetBy = Automatic

	column Tipo
		lineageTag: bdd6eafb-f799-4f99-ac1a-5bedaed76333
		summarizeBy: none
		isNameInferred
		sourceColumn: [Tipo]

		annotation SummarizationSetBy = Automatic

	column Origem
		lineageTag: 80c7912e-4efd-443b-a7c1-8f884d4ec88d
		summarizeBy: none
		isNameInferred
		sourceColumn: [Origem]

		annotation SummarizationSetBy = Automatic

	column Expressao
		lineageTag: ac4ab727-750a-4f13-b028-d7640a01417d
		summarizeBy: none
		isNameInferred
		sourceColumn: [Expressao]

		annotation SummarizationSetBy = Automatic

	column 'Lineage Tag'
		lineageTag: a47accfb-05f7-477c-b0ed-e98b32f64a47
		summarizeBy: none
		isNameInferred
		sourceColumn: [Lineage Tag]

		annotation SummarizationSetBy = Automatic

	partition 'Catálogo de Dados' = calculated
		mode: import
		source =
				
				VAR colunas =
				    SELECTCOLUMNS (
				        FILTER (
				            INFO.VIEW.COLUMNS (),
				            [Table] <> "Catálogo de Dados"
				                && NOT ( [IsHidden] )
				        ),
				        "Categoria", "Coluna",
				        "Nome", [Name],
				        "Descricao", IF(
				                        [IsKey],
				                        "Coluna chave de relacionamento.",
				                        ""
				                    ) & [Description],
				        "Tipo", [DataType] & " - " & [DataCategory],
				        "Origem", [Table],
				        "Expressao", [Expression],
				        "Lineage Tag", [LineageTag]
				    )
				VAR medidas =
				    SELECTCOLUMNS (
				        FILTER (
				            INFO.VIEW.MEASURES (),
				            [Table] <> "Catálogo de Dados"
				                && NOT ( [IsHidden] )
				        ),
				        "Categoria", "Medida",
				        "Nome", [Name],
				        "Descricao", [Description],
				        "Tipo", [FormatStringDefinition],
				        "Origem", [Table],
				        "Expressao", [Expression],
				        "Lineage Tag", [LineageTag]
				    )
				VAR tabelas =
				    SELECTCOLUMNS (
				        FILTER (
				            INFO.VIEW.TABLES (),
				            [Name] <> "Catálogo de Dados"
				                && [Name] <> "Calculations"
				                && NOT ( [IsHidden] )
				        ),
				        "Categoria", "Tabela",
				        "Nome", [Name],
				        "Descricao", [Description],
				        "Tipo", SWITCH (
				                TRUE (),
				                NOT (ISBLANK ( [Expression] )), "Tabela calculada",
				                "Tabela de dados"
				            ),
				        "Origem",[StorageMode],
				        "Expressao", [Expression],
				        "Lineage Tag", [LineageTag]
				    )
				VAR relacionamentos =
				    SELECTCOLUMNS (
				        INFO.VIEW.RELATIONSHIPS (),
				        "Categoria", "Relacionamento",
				        "Nome", [Relationship],
				        "Descricao", IF(
				                        [IsActive],
				                        "Relacionamento Ativo.",
				                        "Relacionamento Inativo."
				                    ),
				        "Tipo", [FromCardinality] & " To " & [ToCardinality] & " - " & [CrossFilteringBehavior],
				        "Origem",
				            "De: " & [FromTable] & "    Para: " & [ToTable],
				        "Expressao", BLANK (),
				        "Lineage Tag", [Name]
				    )
				RETURN
				    UNION (
				        colunas,
				        medidas,
				        tabelas,
				        relacionamentos
				    )

	annotation PBI_Id = 8201948048de4267af5d5cd1a3625d84

